PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  bo:   <http://www.bacillondex.org#>
PREFIX  owl:  <http://www.w3.org/2002/07/owl#>
PREFIX  rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX  so:   <http://purl.org/obo/owl/SO#>
PREFIX  sbol: <http://sbols.org/sbol.owl#>
PREFIX  sybio: <http://www.sybio.ncl.ac.uk#>
PREFIX  fn: <http://www.w3.org/2005/xpath-functions#>

CONSTRUCT
{
    ?SequenceFeatureResource a sbol:DnaComponent ;
                   a ?SOClass ;
                   a ?SequenceFeature ;
                   sbol:dnaSequence ?DnaSequence ;
                   rdfs:label ?name ;
                   rdfs:comment ?description .                   
    ?DnaSequence  a sbol:DnaSequence ;
                    sbol:nucleotides ?NA .
}
WHERE
  {
   OPTIONAL {?SequenceFeature rdfs:label ?name . }
   OPTIONAL {?SequenceFeature rdfs:comment ?description . }
   ?SequenceFeature rdfs:subClassOf  ?NARestriction ;
                    rdfs:subClassOf  ?SuperClass .
   ?NARestriction rdf:type            owl:Restriction ;
                  owl:onProperty      sybio:NA ;
                  owl:hasValue ?NA .
   ?SuperClass rdfs:subClassOf ?SOClass .

  LET ( ?DnaSequence := IRI(fn:concat(str(?SequenceFeature),"_sbolsequence")))
  LET ( ?SequenceFeatureResource := IRI(fn:concat(str(?SequenceFeature),"_sbol")))
 
  FILTER (regex(str(?SOClass) , "http://purl.org/obo/owl/SO#"))
  }
  